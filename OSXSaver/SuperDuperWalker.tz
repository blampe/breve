# SuperDuperWalker: A system to support experiments on the evolution of locomotion.
# Adapted from SuperWalker, by Jon Klein.
# Adaptations by Kyle Harrington and Lee Spector, 2004.
# Contact: lspector@hampshire.edu.
#
# This system differs from SuperWalker primarly in the following ways:
# - Many more attributes of the agents are under evolutionary control.
# - A graphical user interface (Mac OS X only) allows non-programmer
#   users to configure and run experiments.
#
# To graph data choose "Save Log File..." from the File menu, 
# then Open in a spreadsheet/graphing program.
#

# ORIGINAL SUPERWALKER COMMENT:
# This demo is an enhanced version of the Walker demo in which 
# 1) the limbs are not bounded to move symmetrically and 2) limb
# lengths are evolved along with walking behaviors.
# 
# Both of these changes make the search space much larger meaning
# that evolution goes much more slowly than for the Walker.  It is
# best to let this simulation over night in order to observe real
# walking behaviors.
#

# ORIGINAL WALKER COMMENT: 
# Walker is an attempt to evolve locomotion behavior for a simple 
# creature using a genetic algorithm.  THIS DEMO MAY REQUIRE AT
# LEAST 30 MINUTES BEFORE WALKING BEHAVIORS ARE OBSERVED.  
# It may be best to run this demo overnight.  Additionally, it 
# is possible that no evolution will occur during the course of 
# a simulation, so if you don't see results after an hour or two, 
# start the simulation over again so that a new random population 
# is used.  Remember that the behaviors are evolved starting with 
# completely random individuals.
#
# The algorithm can be described as thirty monkeys attempting to learn 
# to drive a bus--they take turns one at a time, breeding the best 
# individuals together to replace the worst individuals.
#
# We place the physical creature in the world and then attempt to evolve
# individuals (non-physical objects in this simulation) which will control
# the physical creature.  
#
# The algorithm is described in the steps below:
#   1) create a number of random individuals
#   2) pick four individuals at random
#   3) let each individual control the body and record how far it moves
#   4) sort the four best individuals based on which could go the furthest
#   5) breed the best two individuals, create two offspring which will 
#      replace the two worst individuals.
#   6) return to step 2 and repeat.
#
# The creatures quickly learn to move by dragging the body along the 
# ground and after longer simulations, realistic "walking" has been
# observed.
#


@use PhysicalControl.
@use Shape.
@use Genome.
@use Link.
@use Stationary.
@use MultiBody.

@define SPEED_K    20.

Controller Walker.

PhysicalControl : Walker {
    + variables:
        state (object).
        seats (list).
        currentSeat (int).
        wigglyThing (object).
        fitness (float).

    + to init:
        cloudTexture (object).
        floorShape, centerShape (object).
        floor, center (object).

        self enable-lighting.
        self enable-smooth-drawing.

        # Create the floor for the critter to walk on.

        floorShape = (new Shape init-with-cube size (1000, 2, 1000)).
        floor = new Stationary.

        floor register with-shape floorShape at-location (0, 0, 0).
        floor catch-shadows.
        
        centerShape = (new Shape init-with-sphere radius 0.2).
        center = new Stationary.
        center register with-shape centerShape at-location (0, 1, 0).
        center set-color to (0, 0, 0).

        cloudTexture = (new Image load from "images/clouds.png").
        self set-background-texture-image to cloudTexture.

        self enable-shadows.
        self enable-reflections.

        self set-background-color to (.4, .6, .9).        

        state = ( self dearchive-xml file "SuperDuperWalkerState.xml" ).

        if !state: {
            state = new WalkerState.
            state set-start-time to (self get-time).
            state re-init.
            print state.
        }

        self pick-drivers.

        # schedule the first driver change and we're ready to go.

        self schedule method-call "change-drivers" at-time (self get-time) + (state get-driver-time).

        self display-current-driver.

        # Create the Creature.

        wigglyThing = new Creature.
        wigglyThing init-with genome (seats{0} get-genome).
        wigglyThing move to (0, 3, 0).
        self offset-camera by (-17, 17, 17).
        self watch item wigglyThing.

    + to display-current-driver:
        current (object).
        fitness (float).
        bestDistance (float).
        runs (int).

        current = seats{currentSeat}.
        fitness = (seats{currentSeat} get-distance).
        runs = (state get-runs).
        bestDistance = (state get-best-distance).

        self set-display-text to "Fitness test #$runs" at-x -.95 at-y -.65 number 1.
        self set-display-text to "Best distance: $bestDistance" at-x -.95 at-y -.75 number 2.
        self set-display-text to "Previous distance: $fitness" at-x -.95 at-y -.85 number 3.

    + to iterate:
        seats{currentSeat} control robot wigglyThing at-time (self get-time).
        fitness = |(wigglyThing get-location)|.
        #self set-interface-item with-id 600 to-string "$fitness".
        (state set-run-time to ((self get-time) - (state get-start-time))).
        #self set-interface-item with-id 800 to-string "$runtime".
		self set-display-text to "Current distance: $fitness" at-x -.95 at-y -.95.
        super iterate.
        


    + to pick-drivers:
        # pick 4 new drivers at random.  we do this by sorting the 
        # list randomly and picking the first 4 items.
        monkeys (list).

        monkeys = (state get-monkeys).

        sort monkeys with random-sort.

        seats{0} = monkeys{0}.
        seats{1} = monkeys{1}.
        seats{2} = monkeys{2}.
        seats{3} = monkeys{3}.

        currentSeat = 0.

    + to random-sort objectA a (object) objectB b (object):
        avoid-warning (object). # a hack to avoid warnings about not using a or b
        avoid-warning = a.
        avoid-warning = b.
        return random[2] - 1.

    + to change-drivers:
        newDriver (int).
        newOffset (vector).
        distance (float).

        # pick a new camera angle and pan...

        newOffset = random[(30, 6, 30)] + (-15, 1, -15).
        if |newOffset| < 15: newOffset = 20 * newOffset/|newOffset|.
        #self bullet-pan-camera-offset by newOffset steps 40.
        self pan-camera-offset by newOffset steps 40.

        # we change the drivers every time a monkey is finished it's 
        # turn.  if we have seen the last monkey, breed them together.

        distance = (|(wigglyThing get-location)::x| + |(wigglyThing get-location)::z|).
        seats{currentSeat} set-distance to distance.
        if distance > (state get-best-distance): {
          state set-best-distance to distance.
        }

        currentSeat += 1.

        if currentSeat > 3: {
            self breed-new-monkeys.
            self pick-drivers.
        }

        newDriver = (seats{currentSeat} get-number).

        if wigglyThing: {
            free wigglyThing.
        }

        wigglyThing = new Creature.
        wigglyThing init-with genome (seats{currentSeat} get-genome).
        wigglyThing move to (0, 3, 0).
        self watch item wigglyThing.

        # schedule a new driver change in 20 seconds.

        self schedule method-call "change-drivers" at-time (self get-time) + (state get-driver-time).
        self display-current-driver.
        state set-runs to ((state get-runs) + 1).
        state save.

    + to breed-new-monkeys:

        sort seats with compare-distance.

        # breed the two best twice, replacing the two worst.

        seats{0} breed with seats{1} to-child seats{2}.
        seats{1} breed with seats{0} to-child seats{3}.

        # give each individual a mutation

        (seats{2} get-genome) mutate.
        (seats{3} get-genome) mutate.

    + to compare-distance of a (object) with b (object):
        result (float).

        result = (b get-distance) - (a get-distance).
        return result.

        # the following methods are accessed from the simulation menu.

    + to get-state:
        return state.
}

Object : Monkeys {
    + variables:
        distanceTraveled (float).
        genome (object).

        number (int).

    + to set-number to n (int):
        number = n.

    + to get-number:
        return number.

    + to init:
        genome = new MonkeyGenome.
        genome randomize.

        self add-dependency on genome.

    + to get-genome:
        return genome.

    + to breed with otherMonkey (object) to-child child (object):
        (child get-genome) crossover from-parent-1 (otherMonkey get-genome) from-parent-2 (self get-genome).

    + to control robot theRobot (object) at-time t (float):
        n (int).

        while n < ((genome get-numLimbs) * (genome get-numSegments)): {
            theRobot set-joint-velocity number n to SPEED_K * (genome calculate-torque number n at t).
            n+=1.
        }

    + to set-distance to value (float):
        distanceTraveled = value.

    + to get-distance:
        return distanceTraveled.
}

Object : WalkerState {
    + variables:
        totalMonkeys (int).
        driverTime (float).
        maxLimbs (int).  
        maxSegments (int).
        maxWidth (int).
        maxLength (int).
        mutationrate (float).

        mwaveCompression (int).
        mphaseShifts (int).
        mampShifts (int).
        mlimbLengths (int).
        mnumLimbs (int).
        mnumSegments (int).
        mwidth (int).
        mlength (int).
        mlimbPoints (int).

        starttime (float).
        runtime (float).
        runs (int).
        bestDistance (float).
        monkeys (list).

    + to init:
        # DEFAULT PARAMETER VALUES, supplied to variables AND GUI elements
        totalMonkeys = 8.
        driverTime = 15.0.
        maxLimbs = 4.
        maxSegments = 4.
        maxWidth = 3.0.
        maxLength = 3.0.
        mutationrate = 0.2.

        #Mutation Flags
        mwaveCompression = 1.
        mphaseShifts = 1.
        mampShifts = 1.
        mlimbLengths = 1.
        mnumLimbs = 1.
        mnumSegments = 1.
        mwidth = 1.
        mlength = 1.
        mlimbPoints = 1.

        runs = 1.

    + to re-init:
        number (int).
        item (object).

        monkeys = totalMonkeys new Monkeys.

        foreach item in monkeys: {
            self add-dependency on item.
            (item set-number to number).
            number += 1.
        }

    + to get-monkeys:
        return monkeys.

    + to get-maxlimbs:
        return maxLimbs.  

    + to get-maxsegments:
        return maxSegments.

    + to get-maxwidth:
        return maxWidth.

    + to get-maxlength:
        return maxLength.

    + to get-mwaveCompression:
        return mwaveCompression.

    + to get-mphaseShifts:
        return mphaseShifts.

    + to get-mampShifts:
        return mampShifts.

    + to get-mlimbLengths:
        return mlimbLengths.

    + to get-mnumLimbs:
        return mnumLimbs.

    + to get-mnumSegments:
        return mnumSegments.

    + to get-mwidth:
        return mwidth.

    + to get-mlength:
        return mlength.

    + to get-mlimbPoints:
        return mlimbPoints.

    + to get-mutationrate:
        return mutationrate.

    + to get-driver-time:
        return driverTime.

    + to get-start-time:
        return starttime.

    + to get-total-monkeys:
        return totalMonkeys.

    + to get-runs:
        return runs.
        
    + to get-best-distance:
        return bestDistance.

    + to set-maxlimbs to value (int):
        maxLimbs = value.

    + to set-maxsegments to value (int):
        maxSegments = value.

    + to set-maxwidth to value (int):
        maxWidth = value.

    + to set-maxlength to value (int):
        maxLength = value.

    + to set-mwaveCompression to value (int):
        mwaveCompression = value.

    + to set-mphaseShifts to value (int):
        mphaseShifts = value.

    + to set-mampShifts to value (int):
        mampShifts = value.

    + to set-mlimbLengths to value (int):
        mlimbLengths = value.

    + to set-mnumLimbs to value (int):
        mnumLimbs = value.

    + to set-mnumSegments to value (int):
        mnumSegments = value.

    + to set-mwidth to value (int):
        mwidth = value.

    + to set-mlength to value (int):
        mlength = value.

    + to set-mlimbPoints to value (int):
        mlimbPoints = value.

    + to set-mutationrate to value (float):
        mutationrate = value.

    + to set-driver-time to value (float):
        driverTime = value.

    + to set-start-time to value (float):
        starttime = value.

    + to set-total-monkeys to value (int):
        totalMonkeys = value.

    + to set-run-time to time (float):
        runtime = time.

    + to set-runs to value (int):
        runs = value.
        
    + to set-best-distance to value (float):
        bestDistance = value.

    + to save:
        self archive-as-xml file "SuperDuperWalkerState.xml".
}

Genome : MonkeyGenome {
    + variables:
        #Mutatable
        waveCompression (float).
        phaseShifts (list).
        ampShifts (list).
        limbLengths (list).
        numLimbs (int).
        numSegments (int).
        limbPoints (list).
        width (float).
        length (float).
        #Static
        maxLimbs (int).
        maxSegments (int).
        maxWidth (int).
        maxLength (int).
        #information

    + to set-maxLimbs to value (int):
        maxLimbs = value.

    + to set-maxSegments to value (int):
        maxSegments = value.

    + to set-maxWidth to value (float):
        maxWidth = value.

    + to set-maxLength to value (float):
        maxLength = value.

    + to randomize:
        n (int).
        state (object).

        state = controller get-state.

        maxLimbs = (state get-maxlimbs).
        maxSegments = (state get-maxsegments).   
        maxWidth = (state get-maxwidth).
        maxLength = (state get-maxlength).     
        #numLimbs = random[maxLimbs - 1] + 1.
        numLimbs = 4.
        numSegments = random[maxSegments - 2] + 2.

        width = random[maxWidth - 1.0] + 1.0.
        length = random[maxLength - 1.0] + 1.0.

        for n=0, n<(numLimbs*numSegments), n++: {
            push (random[6.3] - 3.15) onto phaseShifts.
            push (random[1.0] - .5) onto ampShifts.
            push (random[2.0] + .5) onto limbLengths.
            push (self get-random-point-on-edge) onto limbPoints.
        }

        waveCompression = random[5.0] - 2.5.

    + to calculate-torque number jointNum (int) at time (float):
        # calculates the torque for a certain joint number.
        #if ( |phaseShifts| <= jointNum ) || ( |ampShifts| <= jointNum ) : 
        return 0.5 * sin(waveCompression * (time + phaseShifts{jointNum}) - (ampShifts{jointNum})).

    + to get-random-point-on-edge:
        point (vector).
        randomside (int).

        randomside = random[3].

        if randomside==0 : point = (width/2, ( random[length] - length/2 ), 0).
        if randomside==1 : point = (-1 * width/2, ( random[length] - length/2 ), 0).
        if randomside==2 : point = ((random[width] - width/2), length/2, 0).
        if randomside==3 : point = ((random[width] - width/2), -1 * length/2, 0).

        #print "Width: $width Length: $length Point: $point Sidechosen: $randomside".
        return point.

    + to get-limbPoint number n (int):
        self verify-update-self.
        return limbPoints{n}.

    + to get-limb-length number n (int):
        self verify-update-self.
        return limbLengths{n}.

    + to get-width:
        return width.

    + to get-length:
        return length.

    + to get-numLimbs:
        return numLimbs.

    + to get-numSegments:
        return numSegments.

    + to reset-width from oldwidth (float) to newwidth (float):
        n (int).
        t (vector).

        for n=0, n<|limbPoints|, n++: {
            t = limbPoints{n}.
            if t::x == oldwidth/2 :
            t::x = newwidth/2.
            if (-1 * t::x) == oldwidth/2 :
            t::x == -1 * newwidth/2.
            limbPoints{n} = t.
        }

    + to reset-length from oldlength (float) to newlength (float):
        n (int).
        t (vector).

        for n=0, n<|limbPoints|, n++: {
            t = limbPoints{n}.
            if t::y == oldlength/2 :
            t::y = newlength/2.
            if -1 * t::y == oldlength/2 :
            t::y = -1 * newlength/2.
            limbPoints{n} = t.
        }

    + to reset-from-numlimbs from oldnumlimbs (int) to newnumlimbs (int):
        n,m (int).
        #print "Resetting limbs from $oldnumlimbs to $newnumlimbs".
        n = oldnumlimbs - newnumlimbs.

        if n > 0: {
            while ( n > 0 ) : {
                for m=0, m<numSegments, m++: {
                    pop ampShifts.
                    pop phaseShifts.
                    pop limbLengths.
                }
                pop limbPoints.
                n--.
            }
        }
        if n < 0: {
            n = -1 * n.
            while ( n >= 0 ) : {
                for m=0, m < numSegments, m++: {
                    push random[2.0] - 1.0 onto ampShifts.
                    push random[6.3] - 3.15 onto phaseShifts.
                    push random[2.0] + .5 onto limbLengths.
                }
                push (self get-random-point-on-edge) onto limbPoints.
                n--.
            }
        }    
        numLimbs = newnumlimbs.


    + to reset-from-numsegments from oldnumsegments (int) to newnumsegments (int):
        n,m (int).
        #print "Resetting segments from $oldnumsegments to $newnumsegments".
        n = oldnumsegments - newnumsegments.

        if n > 0: {
            while ( n > 0 ) : {
                for m=0, m<numSegments, m++: {
                    pop ampShifts.
                    pop phaseShifts.
                    pop limbLengths.
                }
                pop limbPoints.
                n--.
            }
        }
        if n < 0: {
            n = -1 * n.
            while ( n >= 0 ) : {
                for m=0, m < numSegments, m++: {
                    push random[2.0] - 1.0 onto ampShifts.
                    push random[6.3] - 3.15 onto phaseShifts.
                    push random[2.0] + .5 onto limbLengths.
                }
                push (self get-random-point-on-edge) onto limbPoints.
                n--.
            }
        }    
        numSegments = newnumsegments.

    + to verify-update-self:
        change (int).
        temp (list).

        change = numLimbs * numSegments.
        if change == 1 : numSegments = 2.

        temp = limbLengths.
        if change != |temp| : {
            while change < |temp| :    pop temp.
            while change > |temp| : push random[2.0] + .5 onto temp.
        }
        limbLengths = temp.
        temp = phaseShifts.
        if change != |temp| : {
            while change < |temp| :    pop temp.
            while change > |temp| : push random[6.3] - 3.15 onto temp.
        }
        phaseShifts = temp.
        temp = ampShifts.
        if change != |temp| : {
            while change < |temp| :    pop temp.
            while change > |temp| : push random[2.0] - 1.0 onto temp.
        }
        ampShifts = temp.

        change = numLimbs.
        temp = limbPoints.
        if change != |temp| : {
            while change < |temp| :    pop temp.
            while change > |temp| : push (self get-random-point-on-edge) onto temp.
        }
        limbPoints = temp.            


    + to mutate:
        n (int).
        oldwidth (float).
        oldlength (float).
        oldnumlimbs (int).
        oldnumsegments (int).
        mutationrate (float).
        state (object).

        state = controller get-state.
        
        mutationrate = (state get-mutationrate).


        #print "Mutate start".


        #print "mutating item $n of $self".

        self verify-update-self.
        if (state get-mlimbPoints) && (random[1.0] < mutationrate) : {
            n = random[|limbPoints| - 1].
            limbPoints{n} = (self get-random-point-on-edge).
        }
        if (state get-mwidth) && (random[1.0] < mutationrate) : {
            oldwidth = width.
            width = (oldwidth + (random[2.0] - 1.0)).
            if width < 1.0: width = 1.0.
            else if width > 4.0: width = 4.0.
            self reset-width from oldwidth to width.
        }
        if (state get-mlength) && (random[1.0] < mutationrate) : {
            oldlength = length.
            length = (oldlength + (random[2.0] - 1.0)).
            if length < 1.0: length = 1.0.
            else if length > 4.0: length = 4.0.
            self reset-length from oldlength to length.
        }
        if (state get-mnumLimbs) && (random[1.0] < mutationrate) : {
            oldnumlimbs = numLimbs.
            numLimbs = (oldnumlimbs + (random[2] - 1)).
            if numLimbs < 1: numLimbs = 1.
            else if numLimbs > maxLimbs: numLimbs = maxLimbs.
            self reset-from-numlimbs from oldnumlimbs to numLimbs.
        }
        if (state get-mnumSegments) && (random[1.0] < mutationrate) : {
            oldnumsegments = numSegments.
            numSegments = (oldnumsegments + (random[2] - 1)).
            if numSegments < 2: numSegments = 2.
            else if numSegments > maxSegments: numSegments = maxSegments.
            self reset-from-numsegments from oldnumsegments to numSegments.
        }
        if (state get-mampShifts) && (random[1.0] < mutationrate) : {
            n = random[|ampShifts| - 1].
            ampShifts{n} = random[2.0] - 1.0.
        }
        if (state get-mphaseShifts) && (random[1.0] < mutationrate) : {
            n = random[|phaseShifts| - 1].
            phaseShifts{n} = random[6.3] - 3.15.
        }
        if (state get-mlimbLengths) && (random[1.0] < mutationrate) : {
            n = random[|limbLengths| - 1].
            limbLengths{n} = random[2.0] + .5.
        }
        if (state get-mwaveCompression) && (random[1.0] < mutationrate) : {
            waveCompression = waveCompression + (random[1.0] - 0.5).
            if waveCompression < -2.5: waveCompression = -2.5.
            else if waveCompression > 2.5: waveCompression = 2.5.
        }

    + to get-points with-q q(int) with-m m (int) with-t t (vector):
        if (t::x>=0) && (t::y<=0) && (q==1): 
        return ( (0, (self get-limb-length number 0) / 2, 0) ).
        if (t::x>=0) && (t::y<=0) && (q==2):
        return ( -1 * (0, (self get-limb-length number m) / 2, 0)).
        if (t::x>=0) && (t::y<=0) && (q==3):
        return ( (0, (self get-limb-length number ( m )) / 2, 0)).

        if (t::x<=0) && (t::y<=0) && (q==1): 
        return ( (0, (self get-limb-length number 0) / 2, 0) ).
        if (t::x<=0) && (t::y<=0) && (q==2):
        return ( -1 * (0, (self get-limb-length number m) / 2, 0)).
        if (t::x<=0) && (t::y<=0) && (q==3):
        return ( (0, (self get-limb-length number ( m )) / 2, 0)).

        if (t::x>=0) && (t::y>=0) && (q==1): 
        return ( -1 * (0, (self get-limb-length number 0) / 2, 0) ).
        if (t::x>=0) && (t::y>=0) && (q==2):
        return ((0, (self get-limb-length number m) / 2, 0)).
        if (t::x>=0) && (t::y>=0) && (q==3):
        return ( -1 * (0, (self get-limb-length number ( m )) / 2, 0)).

        if (t::x<=0) && (t::y>=0) && (q==1): 
        return ( -1 * (0, (self get-limb-length number 0) / 2, 0) ).
        if (t::x<=0) && (t::y>=0) && (q==2):
        return ((0, (self get-limb-length number m) / 2, 0)).
        if (t::x<=0) && (t::y>=0) && (q==3):
        return ( -1 * (0, (self get-limb-length number ( m )) / 2, 0)).
}

MultiBody : Creature {
    + variables:
        bodyLink (object).
        links (list).
        joints (list).

    + to get-root:
        return bodyLink.

    + to init-with genome g (object):
        counter (int).
        bodyShape (object).
        width,length (float).
        numLimbs,numSegments (int).
        m,n (int).

        self add-menu named "Send to Center" for-method "center".
        width = (g get-width).
        length = (g get-length).
        numLimbs = (g get-numLimbs).
        numSegments = (g get-numSegments).

        # we want the limb volume to be constant at 16, but the dimensions can change.

        bodyShape = new Shape.
        bodyShape init-with-cube size (width, length, .4).

        counter = 0.

        links = (numLimbs*numSegments) new Links.
        joints = (numLimbs*numSegments) new RevoluteJoints.

        for n=0, n<numLimbs, n++: {
            for m=0, m<numSegments, m++: {
                links{ (n * numSegments) + m } set-shape to ( (new Shape) init-with-cube size (( sqrt(.16/ (g get-limb-length number m) ) ), (g get-limb-length number m), ( sqrt(.16/ (g get-limb-length number m) ) ))).
            }
        }

        links set-color to random[(1.0, 1.0, 1.0)].

        bodyLink = new Link.
        bodyLink set-shape to bodyShape.

        self register with-link bodyLink.

        for n=0, n<numLimbs, n++: {
            joints{n * numSegments} link parent bodyLink to-child links{n * numSegments}
            with-normal (0,0,1)
            with-parent-point (bodyShape get-point-on-shape on-vector (g get-limbPoint number n))
            with-child-point (g get-points with-q 1 with-m 0 with-t (g get-limbPoint number n)).
            for m=1, m<numSegments, m++: {
                joints{ (n * numSegments) + m } link parent links{ (n * numSegments) + m - 1 } to-child links{ (n * numSegments) + m}
                with-normal (1,0,0)
                with-parent-point (g get-points with-q 2 with-m m with-t (g get-limbPoint number n))
                with-child-point (g get-points with-q 3 with-m ( m - 1 ) with-t (g get-limbPoint number n)).
            }
        }

        # rotate the creature and move it to above the origin.

        self rotate around-axis (1, 0, 0) by 1.57.

        joints set-double-spring with-strength 200 with-max 0.6 with-min -0.6.
        joints set-strength-limit to 250.

        self enable-self-collisions.

        # to center the object, we set the X and Z components to 0, but not
        # the Y, otherwise we would push the walker into the ground

    + to center:
        currentLocation (vector).

        currentLocation = (self get-location).
        self move to (0, currentLocation::y, 0).

        # The following four method allow external objects to manipulate 
        # the torque values of the links.

    + to set-joint-velocity number jointNum (int) to value (float):
        joints{jointNum} set-joint-velocity to value.
}
